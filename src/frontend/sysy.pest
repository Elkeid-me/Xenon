// Copyright (C) 2024 Elkeid-me
//
// This file is part of Xenon.
//
// Xenon is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.
//
// Xenon is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.
//
// You should have received a copy of the GNU General Public License
// along with Xenon.  If not, see <http://www.gnu.org/licenses/>.

WHITESPACE = _{ " " | "\t" | NEWLINE }
identifier = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }

translation_unit = _{ SOI ~ global_item+ ~ EOI }

global_item = _{ all_definitions | function_definition }

function_definition           =  { signature ~ block }
signature                     =  { function_return_type ~ identifier ~ "(" ~ parameter_list ~ ")" }
parameter_list                =  { parameter_definition ~ ("," ~ parameter_definition)* | "void" | "" }
parameter_definition          = _{ pointer_parameter_definition | variable_parameter_definition }
pointer_parameter_definition  =  { int_keyword ~ identifier ~ "[" ~ "]" ~ array_lengths? }
variable_parameter_definition =  { int_keyword ~ identifier }

int_keyword      = @{ "int" ~ !(ASCII_ALPHANUMERIC | "_") }
const_keyword    = @{ "const" ~ !(ASCII_ALPHANUMERIC | "_")}
void_keyword     = @{ "void" ~ !(ASCII_ALPHANUMERIC | "_") }
return_keyword   = @{ "return" ~ !(ASCII_ALPHANUMERIC | "_") }
continue_keyword = @{ "continue" ~ !(ASCII_ALPHANUMERIC | "_") }
break_keyword    = @{ "break" ~ !(ASCII_ALPHANUMERIC | "_") }

const_definition_type = _{ (const_keyword ~ int_keyword) | (int_keyword ~ const_keyword) }

function_return_type = _{ int_keyword | void_keyword }

const_definitions         = _{ const_definition_type ~ const_definition ~ ("," ~ const_definition)* }
const_definition          = _{ const_array_definition | const_variable_definition }
const_variable_definition =  { identifier ~ "=" ~ expression }
const_array_definition    =  { identifier ~ array_lengths ~ "=" ~ initializer_list }

definitions         = _{ int_keyword ~ definition ~ ("," ~ definition)* }
definition          = _{ array_definition | variable_definition }
variable_definition =  { identifier ~ ("=" ~ expression)? }
array_definition    =  { identifier ~ array_lengths ~ ("=" ~ initializer_list)? }

array_lengths = _{ array_subscript }

initializer_list      =  { "{" ~ (initializer_list_item ~ ("," ~ initializer_list_item)*)? ~ "}" }
initializer_list_item = _{ initializer_list | expression}

primary = _{ integer_hex | integer_bin | integer_oct | integer_dec | function_call | array_element | identifier | "(" ~ expression ~ ")" }
atom    = _{ prefix_operator* ~ primary ~ postfix_operator* }

postfix_operator      = _{ postfix_self_increase | postfix_self_decrease }
postfix_self_increase =  { "++" }
postfix_self_decrease =  { "--" }

prefix_operator      = _{ prefix_self_increase | prefix_self_decrease | logical_not | negative | positive | bit_not }
prefix_self_increase =  { "++" }
prefix_self_decrease =  { "--" }
logical_not          =  { "!" }
negative             =  { "-" }
positive             =  { "+" }
bit_not              =  { "~" }

infix_operator  = _{ custom_operator | three_char_operator | two_char_operator | single_char_operator }
custom_operator = ${ "`" ~ identifier ~ "`" }

single_char_operator = _{ multiply | divide | modulus | add | subtract | bit_xor | bit_and | bit_or | greater | less | assignment }
two_char_operator    = _{ logical_and | logical_or | bit_left_shift | bit_right_shift | equal | not_equal | greater_or_equal
                          | less_or_equal | add_assignment | subtract_assignment | multiply_assignment | divide_assignment
                          | modulus_assignment | bit_and_assignment | bit_or_assignment | bit_xor_assignment }
three_char_operator  = _{ bit_right_shift_assignment | bit_left_shift_assignment }

multiply = { "*" }
divide   = { "/" }
modulus  = { "%" }
add      = { "+" }
subtract = { "-" }

logical_and = { "&&" }
logical_or  = { "||" }

bit_left_shift  = { "<<" }
bit_right_shift = { ">>" }
bit_xor         = { "^" }
bit_and         = { "&" }
bit_or          = { "|" }

equal            = { "==" }
not_equal        = { "!=" }
greater          = { ">" }
greater_or_equal = { ">=" }
less             = { "<" }
less_or_equal    = { "<=" }

assignment                 = { "=" }
add_assignment             = { "+=" }
subtract_assignment        = { "-=" }
multiply_assignment        = { "*=" }
divide_assignment          = { "/=" }
modulus_assignment         = { "%=" }
bit_and_assignment         = { "&=" }
bit_or_assignment          = { "|=" }
bit_xor_assignment         = { "^=" }
bit_left_shift_assignment  = { "<<=" }
bit_right_shift_assignment = { ">>=" }

array_element   = { identifier ~ array_subscript }
array_subscript = { ("[" ~ expression ~ "]")+ }

expression    =  { atom ~ (infix_operator ~ atom)* }
function_call =  { identifier ~ "(" ~ argument_list? ~ ")" }
argument_list = _{ expression ~ ("," ~ expression)* }

return_statement = { return_keyword ~ expression? }
while_statement  = { "while" ~ "(" ~ expression ~ ")" ~ (non_block_block_item_in_if_or_while | block) }
if_statement     = { "if" ~ "(" ~ expression ~ ")" ~ (non_block_block_item_in_if_or_while | block) ~ ("else" ~ (non_block_block_item_in_if_or_while | block))? }

integer_hex = @{ ("0x" | "0X") ~ ASCII_HEX_DIGIT+ }
integer_dec = @{ ASCII_NONZERO_DIGIT ~ ASCII_DIGIT* }
integer_oct = @{ "0" ~ ASCII_OCT_DIGIT* }
integer_bin = @{ ("0b" | "0B") ~ ASCII_BIN_DIGIT+ }

block = { "{" ~ (block | non_block_block_item)* ~ "}" }

statement            = _{ while_statement | if_statement | (continue_keyword | break_keyword | return_statement | expression | "") ~ ";"}
all_definitions      = _{ (const_definitions | definitions) ~ ";" }
non_block_block_item = _{ statement | all_definitions }

definitions_in_if_or_while_non_block =  { (const_definitions | definitions) ~ ";" }
empty_statement                      =  { ";" }
non_block_block_item_in_if_or_while  = _{ empty_statement | statement | definitions_in_if_or_while_non_block }
